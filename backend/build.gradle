// App Engine Backend build file

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.4.5'
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'war'

apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: 'com.google.cloud.tools.endpoints-framework-server'

apply from: 'appengine-settings.gradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// This unfortunately doesn't work for AppEngine tasks, so it need to be done by changing settings
// File > Settings... > Build, Execution, Deployment > Build Tools > Gradle > Gradle JDK > 1.8
//java { toolchain {languageVersion = JavaLanguageVersion.of(8) } }

//TODO: compileJava {options.encoding = "UTF-8"}

dependencies {

    implementation 'com.google.endpoints:endpoints-framework:2.2.2'
    implementation 'javax.servlet:servlet-api:2.5'
    //noinspection GradleDependency v6.0 breaks local dev environment
    implementation 'com.googlecode.objectify:objectify:5.1.25' //TODO v6.0
    implementation 'org.jsoup:jsoup:1.15.4'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'

}

war {
    filesMatching('WEB-INF/appengine-web.xml') {
        expand 'fcmKey': fcmServerKey, 'email': adminEmail
    }
}

/*
Make sure you are authenticated with the correct account,
if there's a Permissions error during AppEngine deploy.
Run command: gcloud auth list
and then: gcloud config set account `ACCOUNT`
*/
appengine {
    run {
        host = '0.0.0.0'
        automaticRestart = true
        //TODO: this doesn't work, so the jvmFlag has to be used instead
        //datastorePath = "$rootDir/backend/local_db.bin"
        jvmFlags = [
                "-Ddatastore.backing_store=$rootDir/backend/local_db.bin".toString(),
                "-Dappengine.fullscan.seconds=5".toString(),
                //"-Xdebug".toString(), "-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005".toString()
        ]
    }
    deploy {
        projectId = "$googleCloudProjectId"
        version = 40
        // keep the old version running, do the switch manually in administration
        stopPreviousVersion = false
        promote = false
    }
}

compileTestJava {
    options.encoding = "UTF-8"
}

test {
    useJUnitPlatform()
}

task reloadApp(type: Copy) {
    dependsOn war
    project.afterEvaluate {
        into project.tasks.explodeWar.explodedAppDirectory
        with war
    }
}
