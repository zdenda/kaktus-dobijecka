// Android Application build file

// Android app
apply plugin: 'com.android.application'
// Kotlin
apply plugin: 'kotlin-android'

apply plugin: 'com.google.gms.google-services'
// Firebase Crashlytics
apply plugin: 'com.google.firebase.crashlytics'
// App Engine Endpoints Framework client plugin
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'


android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId "eu.zkkn.android.kaktus"
        minSdkVersion 16
        //noinspection OldTargetApi TODO: Test on Android 11
        targetSdkVersion 29
        versionCode 41
        versionName '0.16.4-Î±1'
        resConfigs "en", "cs"
        vectorDrawables.useSupportLibrary = true
        testApplicationId "eu.zkkn.android.kaktus.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            // SDK 21 is required for native support of Multidex
            // so this might break debug builds on older devices
            multiDexEnabled true
            aaptOptions.cruncherEnabled = false
        }
        beta {
            initWith debug //copy properties from debug
            applicationIdSuffix '.beta'
            // enable resources shrinking, ProGuard and PNG crunching
            shrinkResources true
            minifyEnabled true
            // ProGuard optimization introduces certain risks, so test thoroughly and
            // use it only for beta builds (for now)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            aaptOptions.cruncherEnabled = true
        }
        release {
            // enable shrinking of code and resources
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {

    def work_version = "2.4.0"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.4.2"

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // App Engine Endpoints Framework v2
    endpointsServer project(path: ':backend', configuration: 'endpoints')

    //compile 'com.google.api-client:google-api-client:1.27.0'
    implementation('com.google.api-client:google-api-client-android:1.31.1', {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    })

    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-gcm:$work_version"
    implementation 'com.google.firebase:firebase-core:18.0.0'
    implementation 'com.google.firebase:firebase-config:20.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.3.0'
    implementation 'com.google.firebase:firebase-analytics:18.0.0'
    implementation 'com.google.firebase:firebase-messaging-ktx:21.0.1'
    implementation 'com.google.android.gms:play-services-base:17.5.0'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        //exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

}
