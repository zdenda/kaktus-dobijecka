apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.1'
    defaultConfig {
        applicationId "eu.zkkn.android.kaktus"
        minSdkVersion 11
        //noinspection OldTargetApi - TODO: test on Android O
        targetSdkVersion 25
        versionCode 21
        versionName '0.7.0'
        resConfigs "en", "cs"
        vectorDrawables.useSupportLibrary = true
        testApplicationId "eu.zkkn.android.kaktus.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            aaptOptions.cruncherEnabled = false
        }
        beta {
            initWith debug //copy properties from debug
            applicationIdSuffix '.beta'
            // enable resources shrinking, ProGuard and PNG crunching
            shrinkResources true
            minifyEnabled true
            // ProGuard optimization introduces certain risks, so test thoroughly and
            // use it only for beta builds (for now)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            aaptOptions.cruncherEnabled = true
        }
        release {
            // enable shrinking of code and resources
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {

    // define versions used for multiple dependencies
    def supportLibraryVersion = '25.4.0'
    // Google Play services 10.0.x is the final release that includes full support for
    // Android version 2.3.x. Versions 10.2.x and later require a minimum Android API level of 14,
    // but this project has a minimum level of 11.
    def playServicesVersion = '10.0.1'

    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(path: ':backend', configuration: 'android-endpoints')
    // Android Studio 2.3 doesn't auto import this backend dependency
    compile('com.google.api-client:google-api-client-android:1.20.0', {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    })
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    //noinspection GradleDependency versions 10.2.x and up require a minimum Android API level of 14
    compile "com.google.android.gms:play-services-base:$playServicesVersion"
    //noinspection GradleDependency
    compile "com.google.android.gms:play-services-gcm:$playServicesVersion"
    compile "com.google.firebase:firebase-messaging:$playServicesVersion"
    // Firebase crash reporting only for Beta and Release
    //TODO: Firebase 11.0.0 adds FirebaseCrash.setCrashCollectionEnabled(boolean enable)
    // so it can be disabled for debug. It's even possible to disable it in the app's manifest.
    // https://developers.google.com/android/reference/com/google/firebase/crash/FirebaseCrash.html#setCrashCollectionEnabled%28boolean%29
    betaCompile "com.google.firebase:firebase-crash:$playServicesVersion"
    releaseCompile "com.google.firebase:firebase-crash:$playServicesVersion"
    compile 'com.squareup.picasso:picasso:2.5.2'

    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-crash'
