// Android Application build file

// Android app
apply plugin: 'com.android.application'
// Kotlin
apply plugin: 'kotlin-android'

apply plugin: 'com.google.gms.google-services'
// Firebase Crashlytics
apply plugin: 'com.google.firebase.crashlytics'


android {
    namespace 'eu.zkkn.android.kaktus'
    compileSdk 36
    buildToolsVersion = '36.0.0'
    defaultConfig {
        applicationId "eu.zkkn.android.kaktus"
        minSdkVersion 19
        //noinspection OldTargetApi test on Android 16
        targetSdkVersion 35 // Android 15
        versionCode 52
        versionName '0.21.2-Î±06'
        resourceConfigurations += ['en', 'cs']
        vectorDrawables.useSupportLibrary = true
        testApplicationId "eu.zkkn.android.kaktus.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            // SDK 21 is required for native support of Multidex
            // so this might break debug builds on older devices
            multiDexEnabled true
            aaptOptions.cruncherEnabled = false
        }
        beta {
            initWith debug //copy properties from debug
            applicationIdSuffix '.beta'
            // enable resources shrinking, ProGuard and PNG crunching
            shrinkResources true
            minifyEnabled true
            // ProGuard optimization introduces certain risks, so test thoroughly and
            // use it only for beta builds (for now)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            aaptOptions.cruncherEnabled = true
        }
        release {
            // enable shrinking of code and resources
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig true
    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    def work_version = "2.9.1"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.10.2"

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //noinspection GradleDependency v1.15.0 needs minSdkVersion 21
    implementation 'androidx.core:core-ktx:1.13.1'
    //noinspection GradleDependency v1.7.0 needs minSdkVersion 21
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    //noinspection GradleDependency v2.9.0 needs minSdkVersion 21
    implementation "androidx.lifecycle:lifecycle-common:2.8.7"
    //noinspection GradleDependency v2.2.0 needs minSdkVersion 21
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //noinspection GradleDependency v2.10.0 needs minSdkVersion 21
    implementation "androidx.work:work-runtime:$work_version"
    //noinspection GradleDependency v2.10.0 needs minSdkVersion 21
    implementation "androidx.work:work-runtime-ktx:$work_version"
    //noinspection GradleDependency v2.10.0 needs minSdkVersion 21
    implementation "androidx.work:work-gcm:$work_version"
    //noinspection GradleDependency v18.5.0 needs minSdkVersion 21
    implementation 'com.google.android.gms:play-services-base:18.4.0'
    implementation 'com.google.guava:guava:33.4.8-android'

    // Import the BoM for the Firebase platform
    //noinspection GradleDependency v33.0.0 needs minSdkVersion 21
    implementation platform('com.google.firebase:firebase-bom:32.8.1')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-messaging-ktx'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

}
