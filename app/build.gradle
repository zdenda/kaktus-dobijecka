// Android Application build file

// Android app
apply plugin: 'com.android.application'
// App Engine Endpoints Framework client plugin
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'


android {
    compileSdkVersion 25
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "eu.zkkn.android.kaktus"
        //noinspection MinSdkTooLow
        minSdkVersion 11
        //TODO: Android Oreo: wait until they fix the mess in notifications
        // https://issuetracker.google.com/issues/65726322
        //noinspection OldTargetApi
        targetSdkVersion 25
        versionCode 26
        versionName '0.11.0'
        resConfigs "en", "cs"
        vectorDrawables.useSupportLibrary = true
        testApplicationId "eu.zkkn.android.kaktus.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            aaptOptions.cruncherEnabled = false
        }
        beta {
            initWith debug //copy properties from debug
            applicationIdSuffix '.beta'
            // enable resources shrinking, ProGuard and PNG crunching
            shrinkResources true
            minifyEnabled true
            // ProGuard optimization introduces certain risks, so test thoroughly and
            // use it only for beta builds (for now)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            aaptOptions.cruncherEnabled = true
        }
        release {
            // enable shrinking of code and resources
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {

    // define versions used for multiple dependencies

    // Support library v26: The minimum SDK version has been increased to 14,
    // but this project has a minimum level of 11.
    def supportLibraryVersion = '25.4.0'
    // Google Play services 10.0.x is the final release that includes full support for
    // Android version 2.3.x. Versions 10.2.x and later require a minimum Android API level of 14,
    // but this project has a minimum level of 11.
    def playServicesVersion = '10.0.1'

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // App Engine Endpoints Framework v2
    endpointsServer project(path: ':backend', configuration: 'endpoints')
    //compile 'com.google.api-client:google-api-client:1.23.0'
    implementation('com.google.api-client:google-api-client-android:1.23.0', {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    })

    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    //noinspection GradleDependency versions 10.2.x and up require a minimum Android API level of 14
    implementation "com.google.android.gms:play-services-base:$playServicesVersion"
    //noinspection GradleDependency
    implementation "com.google.android.gms:play-services-gcm:$playServicesVersion"
    //noinspection GradleDependency
    implementation "com.google.firebase:firebase-core:$playServicesVersion"
    //noinspection GradleDependency
    implementation "com.google.firebase:firebase-messaging:$playServicesVersion"
    // Firebase crash reporting only for Beta and Release
    //TODO: Firebase 11.0.0 adds FirebaseCrash.setCrashCollectionEnabled(boolean enable)
    // so it can be disabled for debug. It's even possible to disable it in the app's manifest.
    // https://developers.google.com/android/reference/com/google/firebase/crash/FirebaseCrash.html#setCrashCollectionEnabled%28boolean%29
    //noinspection GradleDependency
    betaImplementation "com.google.firebase:firebase-crash:$playServicesVersion"
    //noinspection GradleDependency
    releaseImplementation "com.google.firebase:firebase-crash:$playServicesVersion"
    implementation 'com.squareup.picasso:picasso:2.5.2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        //exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-crash'
