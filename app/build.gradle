apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "eu.zkkn.android.kaktus"
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 17
        versionName '0.4.8'
        testApplicationId "eu.zkkn.android.kaktus.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        beta {
            initWith debug //copy properties from debug
            applicationIdSuffix '.beta'
            // enable ProGuard
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            // enable ProGuard
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {

    // define versions used for multiple dependencies
    def supportLibraryVersion = '25.2.0'
    // Google Play services 10.0.x is the final release that includes full support for
    // Android version 2.3.x. Versions 10.2.x and later require a minimum Android API level of 14,
    // but this project has a minimum level of 11.
    def playServicesVersion = '10.0.1'

    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(path: ':backend', configuration: 'android-endpoints')
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    //noinspection GradleDependency versions 10.2.x and up require a minimum Android API level of 14
    compile "com.google.android.gms:play-services-base:$playServicesVersion"
    //noinspection GradleDependency
    compile "com.google.android.gms:play-services-gcm:$playServicesVersion"
    compile "com.google.firebase:firebase-messaging:$playServicesVersion"
    // Firebase crash reporting only for Beta and Release
    betaCompile "com.google.firebase:firebase-crash:$playServicesVersion"
    releaseCompile "com.google.firebase:firebase-crash:$playServicesVersion"

    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-crash'
