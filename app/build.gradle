// Android Application build file

// Android app
apply plugin: 'com.android.application'
// Kotlin
apply plugin: 'kotlin-android'

apply plugin: 'com.google.gms.google-services'
// Firebase Crashlytics
apply plugin: 'com.google.firebase.crashlytics'
// App Engine Endpoints Framework client plugin
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'


android {
    compileSdkVersion 33
    buildToolsVersion '33.0.0'
    defaultConfig {
        applicationId "eu.zkkn.android.kaktus"
        minSdkVersion 16
        targetSdkVersion 33 // Android 13
        versionCode 45
        versionName '0.19.0-Î±05'
        resConfigs 'en', 'cs'
        vectorDrawables.useSupportLibrary = true
        testApplicationId "eu.zkkn.android.kaktus.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            // SDK 21 is required for native support of Multidex
            // so this might break debug builds on older devices
            multiDexEnabled true
            aaptOptions.cruncherEnabled = false
        }
        beta {
            initWith debug //copy properties from debug
            applicationIdSuffix '.beta'
            // enable resources shrinking, ProGuard and PNG crunching
            shrinkResources true
            minifyEnabled true
            // ProGuard optimization introduces certain risks, so test thoroughly and
            // use it only for beta builds (for now)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            aaptOptions.cruncherEnabled = true
        }
        release {
            // enable shrinking of code and resources
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {

    def work_version = "2.7.1"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.1"

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // App Engine Endpoints Framework v2
    endpointsServer project(path: ':backend', configuration: 'endpoints')
    implementation ('com.google.api-client:google-api-client:1.32.2', {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    })
    implementation ('com.google.http-client:google-http-client-android:1.40.1', {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    })

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-gcm:$work_version"
    implementation 'com.google.android.gms:play-services-base:18.1.0'

    // Import the BoM for the Firebase platform
    // firebase-bom v29 needs minSdkVersion 19
    implementation platform('com.google.firebase:firebase-bom:28.4.2')
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging-ktx'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        //exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

}
