// Android Application build file

// Android app
apply plugin: 'com.android.application'
// Kotlin
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
// Firebase Crashlytics
apply plugin: 'io.fabric'
// App Engine Endpoints Framework client plugin
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'


android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "eu.zkkn.android.kaktus"
        minSdkVersion 14
        // I don't have any Android P (SDK 28) device to test on
        //noinspection OldTargetApi
        targetSdkVersion 27
        versionCode 33
        versionName '0.13.1'
        resConfigs "en", "cs"
        vectorDrawables.useSupportLibrary = true
        testApplicationId "eu.zkkn.android.kaktus.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            // SDK 21 is required for native support of Multidex
            // so this might break debug builds on older devices
            multiDexEnabled true
            aaptOptions.cruncherEnabled = false
        }
        beta {
            initWith debug //copy properties from debug
            applicationIdSuffix '.beta'
            // enable resources shrinking, ProGuard and PNG crunching
            shrinkResources true
            minifyEnabled true
            // ProGuard optimization introduces certain risks, so test thoroughly and
            // use it only for beta builds (for now)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            aaptOptions.cruncherEnabled = true
        }
        release {
            // enable shrinking of code and resources
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // App Engine Endpoints Framework v2
    endpointsServer project(path: ':backend', configuration: 'endpoints')

    // To avoid conflicts with ListenableFuture
    api 'com.google.guava:guava:28.0-android'

    //compile 'com.google.api-client:google-api-client:1.27.0'
    implementation('com.google.api-client:google-api-client-android:1.30.2', {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    })

    implementation 'androidx.core:core:1.1.0-rc02'
    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-rc01'

    // Firebase Core v15 started requesting a new permission:
    // com.google.android.finsky.permission.BIND_GET_INSTALL_REFERRER_SERVICE
    // https://issuetracker.google.com/issues/78380811
    // so keep both Firebase and Play Services in v12.
    // Unfortunately FCM delivery reports might need v18.0.1
    // https://firebase.google.com/docs/cloud-messaging/understand-delivery#message_delivery_reports
    //noinspection GradleDependency
    implementation 'com.google.firebase:firebase-core:12.0.1'
    //noinspection GradleDependency
    implementation 'com.google.firebase:firebase-messaging:12.0.1'
    //noinspection GradleDependency
    implementation 'com.google.android.gms:play-services-base:12.0.1'
    //noinspection GradleDependency
    implementation 'com.google.android.gms:play-services-gcm:12.0.1'

    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        //exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

}

apply plugin: 'com.google.gms.google-services'
