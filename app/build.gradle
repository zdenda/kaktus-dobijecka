// Android Application build file

// Android app
apply plugin: 'com.android.application'
// Kotlin
apply plugin: 'kotlin-android'

apply plugin: 'com.google.gms.google-services'
// Firebase Crashlytics
apply plugin: 'com.google.firebase.crashlytics'


android {
    namespace 'eu.zkkn.android.kaktus'
    compileSdk 34 //TODO
    buildToolsVersion = '34.0.0' //TODO
    defaultConfig {
        applicationId "eu.zkkn.android.kaktus"
        minSdkVersion 16
        //noinspection OldTargetApi Test on Android 14
        targetSdkVersion 33 // Android 13
        versionCode 51
        versionName '0.21.1-Î±01'
        resourceConfigurations += ['en', 'cs']
        vectorDrawables.useSupportLibrary = true
        testApplicationId "eu.zkkn.android.kaktus.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            // SDK 21 is required for native support of Multidex
            // so this might break debug builds on older devices
            multiDexEnabled true
            aaptOptions.cruncherEnabled = false
        }
        beta {
            initWith debug //copy properties from debug
            applicationIdSuffix '.beta'
            // enable resources shrinking, ProGuard and PNG crunching
            shrinkResources true
            minifyEnabled true
            // ProGuard optimization introduces certain risks, so test thoroughly and
            // use it only for beta builds (for now)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            aaptOptions.cruncherEnabled = true
        }
        release {
            // enable shrinking of code and resources
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig true
    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    def work_version = "2.9.1"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.8.0"

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //noinspection GradleDependency v1.13.0 needs minSdkVersion 19
    implementation 'androidx.core:core-ktx:1.12.0'
    //noinspection GradleDependency v1.7.0 needs minSdkVersion 21
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    //noinspection GradleDependency v2.7.0 needs minSdkVersion 19
    implementation "androidx.lifecycle:lifecycle-common:2.6.2"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.work:work-gcm:$work_version"
    //noinspection GradleDependency v18.5.0 needs minSdkVersion 21
    implementation 'com.google.android.gms:play-services-base:18.4.0'
    implementation 'com.google.guava:guava:33.3.0-android'

    // Import the BoM for the Firebase platform
    //noinspection GradleDependency v29 needs minSdkVersion 19
    implementation platform('com.google.firebase:firebase-bom:28.4.2')
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging-ktx'

    testImplementation 'junit:junit:4.13.2'
    //noinspection GradleDependency v1.6.0 needs minSdkVersion 19
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.5.1', {
        //exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

}
